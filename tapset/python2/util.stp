global pystack;
global ustack;

function extract_string(py_string) {
    return user_string(@cast(py_string, "PyStringObject", @PYTHON_LIBRARY)->ob_sval);
}


function func_name (frame) {
    return extract_string(@cast(frame, "PyFrameObject", @PYTHON_LIBRARY)->f_code->co_name);
}

function filename (frame) {
    return extract_string(@cast(frame, "PyFrameObject", @PYTHON_LIBRARY)->f_code->co_filename);
}


function unpack_pystack () {
    delete pystack
    i = 0;
    frame = current_python_frame()
    while (frame != 0) {
        pystack[i] = sprintf("%s(%s)", func_name(frame), filename(frame));
        frame = @cast(frame, "PyFrameObject", @PYTHON_LIBRARY)->f_back;
        i++;
    }
}

function unpack_ustack () {
    delete ustack;
    i = 0;

    // try throwing the kernel stack in there too
    if (!user_mode()) {
        kbt = backtrace();
        addr = tokenize(kbt, " ");
        while (addr != "") {
            ustack[i] = symname(strtol(addr, 16)) . "_[k]";
            i++;
            addr = tokenize("", " ");
        }
    }

    bt = ubacktrace();
    addr = tokenize(bt, " ");
    while (addr != "") {
        ustack[i] = usymname(strtol(addr, 16));
        i++;
        addr = tokenize("", " ");
    }
}

function current_python_frame() {
    pythreadstate = @var("_PyThreadState_Current@Python/pystate.c", @PYTHON_LIBRARY);
    if (pythreadstate == 0) {
        return 0
    }
    return @cast(pythreadstate, "PyThreadState", @PYTHON_LIBRARY)->frame;
}


function sprint_merged_stack () {
    unpack_pystack();
    unpack_ustack();
    merged_stack = "";
    pystack_idx = 0;
    foreach(idx+ in ustack) {
        if (ustack[idx] == "PyEval_EvalFrameEx") {
            merged_stack = ";" . pystack[pystack_idx] . merged_stack;
            pystack_idx++;
        } else {
            merged_stack = ";" . ustack[idx] . merged_stack;
        }
    }
    return merged_stack;
}

function sprint_pystack () {
    unpack_pystack();
    stack = "";
    foreach (idx+ in pystack) {
        stack = ";" . pystack[idx] . stack;
    }
    return stack;
}
